  10 REM *** GAME OF ACEY-DUCEY WRITTEN BY BILL PALMBY
  20 REM *** ADLAI STEVENSON HIGH SCHOOL, PRAIRIE VIEW, ILL
  30 REM *** TRANSLATED TO RSTS/E BY DAVE AHL, DIGITAL
  40 REM *** TRANSLATED TO JASON'S TINY BASIC BY @THORPEJ
  50 REM *** (FROM 101 BASIC COMPUTER GAMES, DIGITAL, 1975)
  99 REM JTTB DOES NOT YET HAVE THE RANDOMIZE STATEMENT.
 100 REM RANDOMIZE
 101 PRINT "Acey-Ducey is played in the following manner:"
 102 PRINT "The dealer (computer) deals two cards face up."
 103 PRINT "You have the option to bet or not to bed depending"
 104 PRINT "on whether or not you feel the next card will have"
 105 PRINT "a value between the first two."
 106 PRINT "If you do not want to bet, input a 0."
 110 PRINT
 160 N=100
 161 Q=100
 190 PRINT "You now have "; Q; " dollars."
 195 PRINT
 200 GOTO 260
 210 Q=Q+M
 220 GOTO 190
 240 Q=Q-M
 250 GOTO 190
 260 PRINT "Here are your next two cards..."
 270 A=INT(14*RND(0))+2
 280 IF A<2 THEN 270
 290 IF A>14 THEN 270
 300 B=INT(14*RND(0))+2
 310 IF B<2 THEN 300
 320 IF B>14 THEN 300
 330 IF A>=B THEN 270
 350 C=A
 360 GOSUB 2000
 500 C=B
 510 GOSUB 2000
 650 PRINT
 660 INPUT "What is your bet"; M
 670 IF M<>0 THEN 680
 675 PRINT "CHICKEN!!"
 676 PRINT
 677 GOTO 260
 680 IF M<=Q THEN 730
 690 PRINT "Sorry, my friend, but you bet too much."
 700 PRINT "You only have "; Q; " dollars to bet."
 710 GOTO 650
 730 C=INT(14*RND(0))+2
 740 IF C<2 THEN 730
 750 IF C>14 THEN 730
 760 GOSUB 2000
 910 IF C>A THEN 930
 920 GOTO 970
 930 IF C>=B THEN 970
 950 PRINT "YOU WIN!!!"
 960 GOTO 210
 970 PRINT "Sorry, you lose."
 980 IF M<Q THEN 240
1000 PRINT
1010 PRINT "Sorry, friend, but you blew your wad."
1020 INPUT "Try again (yes or no)"; A$
1030 IF A$="yes" THEN 110
1040 PRINT
1041 PRINT "O.K. Hope you had fun!!"
1050 END
2000 REM THIS SUBROUTINE PRINTS THE CARD VALUE.  THE CARD MUST BE IN C.
2010 IF C>=11 THEN 2100
2020 PRINT C
2030 RETURN
2100 GOTO 2120+(C-11)*20
2120 PRINT "Jack"
2130 RETURN
2140 PRINT "Queen"
2150 RETURN
2160 PRINT "King"
2170 RETURN
2180 PRINT "Ace"
2190 RETURN
